plugins {
	id 'java'
	id 'application'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.google.guava:guava:28.2-jre'
	implementation 'org.ow2.asm:asm:9.2'
	implementation 'org.ow2.asm:asm-tree:9.2'

	// Use JUnit Jupiter API for testing.
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'

	// Use JUnit Jupiter Engine for testing.
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

tasks.withType(JavaCompile) {
	options.release.set(16)
}

application {
	mainClass = 'be.seeseemelk.jtsc.App'
}

jar {
	manifest {
		attributes 'Main-Class': 'be.seeseemelk.jtsc.App'
	}
}

test {
	// Use junit platform for unit tests
	useJUnitPlatform()
}

task integration(group: "Verification", description: "Run integration tests") {
	doLast {
		exec {
			workingDir "tests"
			commandLine "make", "test-instrumented"
		}
	}
}

task dtest(group: "Verification", description: "Run j4dlang tests") {
	doLast {
		exec {
			workingDir "j4dlang"
			commandLine "dub", "test", "-q"
		}
	}
}

check {
	it.dependsOn integration
	it.dependsOn dtest
}
