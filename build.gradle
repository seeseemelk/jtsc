plugins {
    id 'java'
    id 'application'
	id 'eclipse'
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    implementation 'com.google.guava:guava:28.2-jre'
	implementation 'org.ow2.asm:asm:9.0'
	implementation 'org.ow2.asm:asm-tree:9.0'

    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

application {
    // Define the main class for the application.
    mainClassName = 'be.seeseemelk.jtsc.App'
}

jar {
	manifest {
		attributes "Main-Class": "be.seeseemelk.jtsc.App"
	}
	
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

task integration(group: "Verification", description: "Run integration tests") {
	it.dependsOn jar
	doLast {
		exec {
			workingDir "tests"
			commandLine "make test-all"
		}
	}
}

task dtest(group: "Verification", description: "Run j4dlang tests") {
	doLast {
		exec {
			workingDir "j4dlang"
			commandLine "dub", "test", "-q"
		}
	}
}

check {
	it.dependsOn integration
	it.dependsOn dtest
}
